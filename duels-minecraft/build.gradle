import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:24.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'net.byteflux:libby-bukkit:1.3.1'
    compileOnly 'commons-lang:commons-lang:2.6'
    compileOnly 'com.mojang:authlib:1.5.25'
    compileOnly 'me.clip:placeholderapi:2.11.7'
    compileOnly 'com.github.sirblobman.api:core:2.9-SNAPSHOT'
    compileOnly 'com.github.timderspieler:DeluxeCombat-API:1.5.1'
    compileOnly 'com.github.sirblobman.combatlogx:api:11.5-SNAPSHOT'
    compileOnly 'com.artillexstudios:AxGraves:1.7.0'
    compileOnly 'com.github.No-Not-Jaden:NotBounties:1.22.17R5'
    compileOnly 'me.chancesd.pvpmanager:pvpmanager:4.0.8'
    compileOnly('net.essentialsx:EssentialsX:2.21.2') {
        transitive = false
    }
    compileOnly(name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    compileOnly name: 'Vault-1.6.7'
    compileOnly name: 'CombatTagPlus'
    compileOnly name: 'Factions-1.6.9.5-U0.1.14'
    compileOnly name: 'MassiveCore'
    compileOnly name: 'Factions'
    compileOnly name: 'MyPet-2.3.4'
    compileOnly name: 'BountyHunters-2.2.6'
    compileOnly name: 'SimpleClans-2.14.4.1'
    compileOnly name: 'LeaderHeadsAPI'

    implementation project(':duels-api')
    implementation project(':duels-core')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v7')
    compileOnly 'io.papermc:paperlib:1.0.8'
    compileOnly 'com.fasterxml.jackson.core:jackson-databind:2.20.1'
    compileOnly 'org.json:json:20251224'
    compileOnly 'net.kyori:adventure-text-minimessage:4.26.1'
    compileOnly 'net.kyori:adventure-text-serializer-legacy:4.26.1'
    compileOnly 'net.kyori:adventure-api:4.26.1'
    compileOnly 'net.kyori:examination-api:1.3.0'
    compileOnly 'net.kyori:adventure-key:4.26.1'
    compileOnly 'net.kyori:adventure-nbt:4.26.1'
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-api'))
        include(project(':duels-core'))
        include(project(':duels-worldguard'))
        include(project(':duels-worldguard-v7'))
        include(dependency('net.byteflux:.*'))
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'net.byteflux.libby', group + 'libby'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}
