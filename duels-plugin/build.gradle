import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:24.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    implementation 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'
    implementation 'commons-lang:commons-lang:2.6'
    implementation 'com.mojang:authlib:1.5.25'
    implementation 'me.clip:placeholderapi:2.11.6'
    // These are compileOnly because runtime presence is provided by the server classpath
    implementation 'com.github.timderspieler:DeluxeCombat-API:1.5.1'
    implementation 'com.artillexstudios:AxGraves:1.7.0'
    implementation 'com.github.No-Not-Jaden:NotBounties:1.22.0'
    implementation('net.essentialsx:EssentialsX:2.21.2') {
        transitive = false
    }
    implementation(name: 'MVdWPlaceholderAPI-3.1.1') {
        transitive = false
    }
    implementation name: 'Vault-1.6.7'
    implementation name: 'CombatTagPlus'
    implementation name: 'PvPManager-3.7.16'
    implementation name: 'Factions-1.6.9.5-U0.1.14'
    implementation name: 'MassiveCore'
    implementation name: 'Factions'
    implementation name: 'MyPet-2.3.4'
    implementation name: 'BountyHunters-2.2.6'
    implementation name: 'SimpleClans-2.14.4.1'
    implementation name: 'LeaderHeadsAPI'

    implementation project(':duels-api')
    implementation project(':duels-worldguard')
    implementation project(':duels-worldguard-v6')
    implementation project(':duels-worldguard-v7')
    implementation project(':duels-common')
    implementation 'io.papermc:paperlib:1.0.8'
    implementation 'space.arim.morepaperlib:morepaperlib:0.4.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation 'org.json:json:20250517'
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = parent.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-api'))
        include(project(':duels-worldguard'))
        include(project(':duels-worldguard-v6'))
        include(project(':duels-worldguard-v7'))
        include(project(':duels-common'))
        include(dependency('com.fasterxml.jackson.core:.*'))
        include(dependency('io.papermc:.*'))
        include(dependency('org.json:.*'))
        include(dependency('space.arim.morepaperlib:.*'))
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson', 'com.meteordevelopments.duels.shaded.jackson'
    relocate 'io.papermc.lib', group + 'paperlib'
    relocate 'space.arim.morepaperlib', group + 'morepaperlib'
}

// To build Duels plugin jar, run './gradlew clean build'.
build {
    dependsOn(shadowJar)
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group.toString()
            artifactId = project.name.toLowerCase()
            version = project.version
            from components.java
        }
    }
}
