import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import org.apache.tools.ant.filters.ReplaceTokens

clean.doFirst {
    delete "$rootDir/out/"
}

tasks.withType(ShadowJar) {
    destinationDir = file("$rootDir/out/")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.yml'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

dependencies {
    compile 'org.projectlombok:lombok:1.16.20'
    compile 'net.milkbowl.vault:VaultAPI:1.6'
    compile 'me.clip:placeholderapi:2.8.4'
    compile('be.maximvdw:MVdWPlaceholderAPI:2.5.1-SNAPSHOT') { transitive = false }
    compile 'com.sk89q:worldguard:6.1.1-SNAPSHOT'
    compile name: 'EssentialsX'
    compile name: 'CombatTagPlus'
    compile name: 'PvPManager'
    compile name: 'Factions-1.6.9.5-U0.1.14'
    compile name: 'MassiveCore'
    compile name: 'Factions'
    compile name: 'MyPet-2.3.4'
    compile name: 'BountyHunters'
    compile name: 'SimpleClans'
    compile name: 'CombatLogX'

    compile project(':duels-api')
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'org.inventivetalent.spiget-update:bukkit:1.4.0'
}

shadowJar {
    archiveName = parent.name + '-' + project.version + '.jar'

    dependencies {
        include(dependency(':duels-api'))
        include(dependency('com.google.code.gson:.*'))
        include(dependency('org.inventivetalent.spiget-update:.*'))
    }
    def group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'org.inventivetalent.update.spiget', group + 'update.spiget'
    relocate 'com.google.gson', group + 'gson'
}

build.dependsOn(shadowJar)